plugins {
	id 'fabric-loom' version '1.7-SNAPSHOT'
	id 'maven-publish'
}

version = getSubprojectVersion(project)
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

dependencies {
	implementation 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered() {
		officialMojangMappings()
	}
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:$project.fabric_version"

	compileOnly "org.jetbrains:annotations:21.0.1"

	// bukkit
	implementation(include("org.yaml:snakeyaml:2.0"))
	implementation(include("com.mohistmc:i18n:0.6"))
	implementation(include('com.mohistmc:json:0.5'))
	implementation(include('com.mohistmc:tools:0.5.1'))
	implementation(include('com.mohistmc:dynamicenum:0.3.1'))
	// jansi support
	implementation(include("org.jline:jline-terminal:3.21.0"))
	implementation(include("org.jline:jline-terminal-jansi:3.21.0"))
	implementation(include("net.minecrell:terminalconsoleappender:1.4.0"))

	// craftbukkit
	implementation "org.fusesource.jansi:jansi:1.18"
	implementation "jline:jline:2.14.6"
	implementation "com.googlecode.json-simple:json-simple:1.1.1"
	implementation "org.xerial:sqlite-jdbc:3.46.0.0"
	implementation 'com.mysql:mysql-connector-j:8.4.0'

	implementation "net.md-5:SpecialSource:1.11.2"

	// spigot
	implementation "net.md-5:bungeecord-chat:1.16-R0.4"
	implementation "io.izzel:tools:1.3.0"

	implementation(include("org.apache.logging.log4j:log4j-iostreams:2.20.0"))

	//class modify
	implementation(include("io.izzel.arclight:mixin-tools:1.2.4")) { transitive = false }
	annotationProcessor("io.izzel.arclight:mixin-tools:1.2.4") { transitive = false }

	// mappings remapper
	implementation(include("net.fabricmc:mapping-io:0.6.1"))

	// mohist lib
	implementation(include("com.mohistmc:librariesvault:1.20.2"))

	include(rootProject.project("banner_translate"))
}

loom {
	accessWidenerPath = file("src/main/resources/banner.accesswidener")
	log4jConfigs.from(file("src/main/resources/log4j2_banner.xml"))
}

loom.runs.configureEach {
	vmArgs([
			"-enableassertions",
			"-Dmixin.debug.verify=true",
			"-Dmixin.debug.countInjections=true"
	])
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
	options.compilerArgs << '-Xlint:-dep-ann' << '-Xlint:-removal' << '-Xdiags:verbose'
}

jar {
	from("LICENSE")
	manifest {
		attributes(
				'Specification-Title'   : 'Banner',
				'Specification-Vendor'  : 'MohistMC',
				'Specification-Version' : ci_version(),
				'Implementation-Title'  : 'Banner',
				'Implementation-Version': version,
				'Implementation-Vendor' : 'MohistMC'
		)
	}
}